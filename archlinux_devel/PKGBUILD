pkgname=easy_speedfan_linux
gitname=easy_speedfan_linux
giturl="https://github.com/tjmnmk/easy_speedfan_linux.git"
pkgver=1.0.2
pkgrel=1
pkgdesc="Automatic fan speed control for Linux based on CPU/GPU temperature sensors."
arch=('any')
url="$giturl"
license=('Beerware')
depends=('python' 'python-cachetools' 'python-loguru' 'lm_sensors')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'git')
source=("git+$giturl")
md5sums=('SKIP')
backup=('etc/easy_speedfan_linux/config.py')

pkgver() {
  cd "$srcdir/$gitname"
  git describe --tags --abbrev=0 | sed 's/^v//'
}

build() {
  cd "$srcdir/$gitname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/$gitname"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 config.py "$pkgdir/etc/easy_speedfan_linux/config.py"
  # Install systemd service file
  install -Dm644 "$srcdir/$gitname/easy_speedfan_linux.service" "$pkgdir/usr/lib/systemd/system/easy_speedfan_linux.service"
}
